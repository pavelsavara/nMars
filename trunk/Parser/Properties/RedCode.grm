"Name"     = 'RedCode'
"Author"   = 'Pavel Savara'
"Version"  = '0.1'
"About"    = 'CoreWars RedCode'

"Start Symbol" = <Start>

! ------------------------------------------------- Sets

{WS}           = {Whitespace} - {CR} - {LF}

! ------------------------------------------------- Terminals

Whitespace    = {WS}+
NewLine       = {CR}{LF} | {CR} | {LF}
{LabelValid}  = {AlphaNumeric} + [_]

Label         = {Letter}{LabelValid}*[:]*
Integer       = {Number}+
Comment Line  =  ';'

! ------------------------------------------------- Rules

<eol> ::= <eolSingle>
        | <eol> <eolSingle>

<eolSingle> ::= NewLine
              | Comment Line

<eolOptional> ::= <eol>
                | !Empty

<Start>   ::= <eolOptional> <AllStatements> <EndOptional>

<EndOptional> ::= <eol> End <eolOptional>
                | <eol> End Label <eolOptional>
                | <eol>
                | !Empty

<AllStatements> ::= <Statement>
                  | <For>
                  | <AllStatements> <eol> <Statement>
                  | <AllStatements> <eol> <For>

<For> ::= <LabelsOptional> for <Expression> <eol> <InnerStatementsOptional> rof
!       | <LabelsOptional> for <Expression> <eol> <AllStatements> rof --- TODO nested for


<InnerStatementsOptional> ::= <InnerStatements> <eol>
                            | !Empty

<InnerStatements> ::= <Statement>
                    | <InnerStatements> <eol> <Statement>

<Statement> ::= <Pin>
              | <Org>
              | <Equ>
              | <LabelsOptional> <Operation>
              | <Labels> <eol> <Operation>

<Pin> ::= Pin <Expression>
<Org> ::= Org Label
<Equ> ::= <Labels> Equ <Expression>
        | <Labels> <eol> Equ <Expression>

<LabelsOptional> ::= <Labels>
                   | !Empty

<Labels> ::= Label 
           | <Labels> <eol> Label 

<Operation> ::= <Operation0>
              | <Operation1>
              | <Operation2>

<Operation0> ::= <Operator0> 
               | <Operator0> <Parameter> ',' <Parameter>
               | <Operator0> '.' <Modifier> <Parameter> ',' <Parameter>


<Operation1> ::= <Operator1> <Parameter>
               | <Operator1> '.' <Modifier> <Parameter>
               | <Operator1> <Parameter> ',' <Parameter>
               | <Operator1> '.' <Modifier> <Parameter> ',' <Parameter>


<Operation2> ::= <Operator2> <Parameter> ',' <Parameter>
               | <Operator2> '.' <Modifier> <Parameter> ',' <Parameter>

<Parameter>   ::= <Expression>
                | <Mode> <Expression>

<Mode> ::= '#' | '$' | '@' | '<' | '>' | '{' | '}' | '*'

<Operator0> ::= Nop 
<Operator1> ::= Jmp | Spl | Dat 
<Operator2> ::= Mov | Add | Sub | Mul | Div | Mod
              | Jmz | Jmn | Djn | Slt | Sne | Ldp | Stp
              | Cmp | Seq 

<Modifier> ::= I | A | B | AB | BA | F | X 

<Expression>  ::= <Expression> '+' <Mult Exp>
               |  <Expression> '-' <Mult Exp>
               |  <Mult Exp> 

<Mult Exp>    ::= <Mult Exp> '*' <Negate Exp>
               |  <Mult Exp> '/' <Negate Exp>
               |  <Mult Exp> '%' <Negate Exp>
!               |  <Mult Exp> '&' <Negate Exp> --TODO and
!               |  <Mult Exp> '|' <Negate Exp> --TODO or, solve priority
               |  <Negate Exp> 

<Negate Exp>  ::= '-' <Value>
                |  <Value>

<Value> ::= Integer
          | Label
          | '(' <Expression> ')'
