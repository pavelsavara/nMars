;redcode-94
;name Shotgun 1.00
;author Eric J. Schwertfeger
;strategy A Jumper with monitor. Jumper bombs with
;strategy JMP PIT instructions, where pit is
;strategy (SPL 0,2;MOV -1,>-1), you figure it out. 
;strategy monitor kills jumper after 3 passes, and starts
;strategy core clear.
;strategy v 1.00 Finally got the obvious bugs worked out :-)
;strategy Submitted: @date@
;assert 1

STEP	EQU	240
OFFS	EQU	10
INC	EQU	30
CSTEP	EQU	24
RSTEP	EQU	-24
RLAUNCH	MOV	#-250-6+RSTEP,RBDST-RSTEP
	MOV	#2+(RSTEP*2),RCDST-RSTEP
	MOV	#-250-6+RSTEP,RBDST
	MOV	#2+(RSTEP*2),RCDST
	SPL	1
	SPL	1
	SPL	1
	JMP	RCSRC	
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
RCSRC	MOV	#8,#0
	MOV	RBDST-RSTEP,>RBDST-RSTEP
	MOV	RBDST-RSTEP,>RBDST-RSTEP
	MOV	RBDST-RSTEP,>RBDST-RSTEP
	MOV	<RCSRC,<RCDST-RSTEP
	JMZ	RCSRC+RSTEP,RCSRC
RCDST	DAT	#0,#RBDST+1+(RSTEP*2)
RBDST	DAT	#-200
DBOMB	DAT	#0,#0
LAUNCH	SPL	JLAUNCH
KDELAY	MOV	#408,#0
	DJN	0,KDELAY
	MOV	DBOMB,TRAP-1
DOIT	SPL	CLAUNCH
WDELAY	MOV	#1950,#0
	DJN	0,WDELAY
	JMP	DOIT
JLAUNCH	MOV	CSRC,CSRC-STEP
	MOV	CDST,CDST-STEP
	SPL	1
	SPL	1
	SPL	1
	JMP	LOOP
BOMBS	JMP	-OFFS,TRAP+OFFS+STEP
	JMP	-OFFS-INC*1,TRAP+OFFS+INC*1
	JMP	-OFFS-INC*2,TRAP+OFFS+INC*2
	JMP	-OFFS-INC*3,TRAP+OFFS+INC*3
	JMP	-OFFS-INC*4,TRAP+OFFS+INC*4
	JMP	-OFFS-INC*5,TRAP+OFFS+INC*5
	JMP	-OFFS-INC*6,TRAP+OFFS+INC*6
	JMP	-OFFS-INC*7,TRAP+OFFS+INC*7
CSRC	DAT	#0,#LAST+1+STEP
CDST	DAT	#0,#LAST+1+STEP*2
LOOP	MOV	<CSRC-STEP,<CDST-STEP
	MOV	<CSRC-STEP,<CDST-STEP
	MOV	>0,@BOMBS
	JMP	LOOP+STEP
TRAP	SPL	0,2
	MOV	-1,>-1
LAST	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
	DAT	#0,#0
CLAUNCH	SPL	RLAUNCH
	MOV	#250-8+CSTEP,CBDST-CSTEP
	MOV	#2+(CSTEP*2),CCDST-CSTEP
	MOV	#250-8+CSTEP,CBDST
	MOV	#2+(CSTEP*2),CCDST
	SPL	1
	SPL	1
	SPL	1
	JMP	CCSRC	
	DAT	#0,#0
CCSRC	MOV	#8,#0
	MOV	CBDST-CSTEP,<CBDST-CSTEP
	MOV	CBDST-CSTEP,<CBDST-CSTEP
	MOV	CBDST-CSTEP,<CBDST-CSTEP
	MOV	<CCSRC,<CCDST-CSTEP
	JMZ	CCSRC+CSTEP,CCSRC
CCDST	DAT	#0,#CBDST+1+(CSTEP*2)
CBDST	DAT	#250-8+CSTEP	
	END	LAUNCH
